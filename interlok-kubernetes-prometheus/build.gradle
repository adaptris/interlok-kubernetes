ext {
  componentName='Interlok Kubernetes/Prometheus pushgateway'
  componentDesc="Pushes metrics into Prometheus via pushgateway"
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
  prometheusVersion = '0.10.0'
}

dependencies {
  compile project(':interlok-kubernetes-metrics')

  compile ("io.prometheus:simpleclient:$prometheusVersion")
  compile ("io.prometheus:simpleclient_hotspot:$prometheusVersion")
  compile ("io.prometheus:simpleclient_httpserver:$prometheusVersion")
  compile ("io.prometheus:simpleclient_pushgateway:$prometheusVersion")

}

jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
               "Build-Jdk": System.getProperty('java.version'),
               "Implementation-Title": componentName,
               "Implementation-Version": project.version,
               "Implementation-Vendor-Id": project.group,
               "Implementation-Vendor": organizationName)
  }
}


task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.extensions.delombokTask
}

artifacts {
  archives javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact sourcesJar { classifier "sources" }

      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", componentDesc)
        asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/pages/advanced/advanced-interlok-scaling")
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "3.9.1+")
        properties.appendNode("license", "false")
        properties.appendNode("tags", "prometheus,kubernetes,metrics,management")
        properties.appendNode("repository", "https://github.com/adaptris/interlok-kubernetes")
        properties.appendNode("readme", "https://github.com/adaptris/interlok-kubernetes/raw/develop/README.md")
        properties.appendNode("deprecated", "Pushgateway is not the preferred way to get metrics into Kubernetes")

      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

task deleteGeneratedFiles(type: Delete) {
  delete 'activemq-data'
  delete 'data'
  delete delombokTargetDir
}

delombok {
  target = delombokTargetDir
}

clean.dependsOn deleteGeneratedFiles
